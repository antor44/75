1 REM *** SEVEN AND A HALF V 1.06 ***
2 REM *** By antor44 ***
5 SCREEN 0,,0:COLOR 15,4,15:KEY OFF:WIDTH 40:CLEAR:W=-2:DI=2:WW=RND(-TIME):PLAY "V8T250S13M50L8O5AA"
10 LOCATE 11,12,0:PRINT"SEVEN AND A HALF":FOR X=1 TO 800:NEXT X:GOSUB 3000
15 CLS:LOCATE 6,12,0:PRINT "1=EASY,2=MEDIUM,3=HARD(*";CHR$(48+DI);")":OK$=INPUT$(1):IF OK$=CHR$(13) AND DI<=3 AND DI>=1 THEN 20 ELSE DI=VAL(OK$):IF OK$<"1" OR OK$>"3" THEN PLAY "O2CCC":DI=1:GOTO 15
20 CLS:LOCATE 1,12,0:PRINT"Choosing who will be the dealer...";:FOR C=1 TO 500:NEXT C:C=RND(1):CLS:PLAY"O5AA":GOSUB 140:IF C<.5 THEN 370 ELSE 200
25 Z=INT(RND(1)*10):C=Z+1:IF Z=10 THEN 25
30 IF A(Z)=0 THEN 25
40 PLAY"O4D":A(Z)=A(Z)-1:IF Z<7 THEN RETURN
50 C=.5:IF Z=7 THEN B$="J":RETURN
60 IF Z=8 THEN B$="Q":RETURN
70 B$="K":RETURN
80 V$=" -You WIN!":I=I+1:IF K=0 AND D>=E THEN V$=" -I win"
90 IF K=1 AND D>E THEN V$=" -I win"
100 LOCATE 0,7,0:PRINT"You:";E;"- Me:";D;V$: IF V$=" -I win" THEN J=J+1:I=I-1:PLAY"T210O4L8CDL4EGGV8T250S13M50L8" ELSE PLAY "T210L2O5E4L8CO4BL4O5CDEF#G2V8T250S13M50L8"
110 FOR GH=1 TO 800:WE$=INKEY$:NEXT GH:CA=0:CO=0:W=-2:H=0:F=0:CLS:IF K=1 THEN 130
120 IF E>D AND E>7 THEN Y=1
130 IF D>E AND D>7 THEN Y=0
140 D=0:E=0:LOCATE 0,0,0:PRINT"You:";I;"- Me:";J
150 M=0:FOR L=0 TO 9:M=M+A(L):NEXT L:IF M>=15 THEN RETURN
160 PRINT"Shuffling...":FOR L=0 TO 9:A(L)=16:NEXT L:M=160:FOR GH=1 TO 1000:NEXT GH:RETURN
170 LOCATE 0,7,0:PRINT E;" - Bust, I win.":PLAY"T210O4L8CDL4EGGV8T250S13M50L8":J=J+1:E=0:GOTO 110
180 LOCATE 0,7,0:PRINT D;" - Bust, you WIN!":PLAY "T210L2O5E4L8CO4BL4O5CDEF#G2V8T250S13M50L8":I=I+1:D=0:GOTO 110
200 LOCATE 0,2,0:PRINT"I'm the dealer.":LOCATE 0,5,0:K=0:Y=0
210 GOSUB 25:E=E+C:IF E>=8 THEN GOSUB 1320:GOSUB 170:IF Y=0 GOTO 200 ELSE GOTO 370
220 IF C<1 THEN LOCATE 0,7,0:PRINT B$;:GOTO 240
230 LOCATE 0,7,0:PRINT CHR$(48+C);
240 PRINT" -Total:";E;"    ";:GOSUB 1320:IF E<=7 THEN LOCATE 0,5,0:PRINT "-Card? ":A$=INPUT$(1):IF A$="Y" OR A$="y" OR A$=CHR$(13) THEN 210
250 IF K=1 THEN GOSUB 80:IF Y=0 GOTO 200 ELSE GOTO 370
260 CO=1:LOCATE 0,7,0:PRINT SPC(39):LOCATE 0,5,0:PRINT"-It's my turn "
270 IF E>7 THEN F=7 ELSE F=E-C:IF F<5 THEN GOTO 380
280 GOSUB 25:D=D+C:IF D>=8 THEN GOSUB 1320:GOSUB 180:IF Y=0 GOTO 200 ELSE GOTO 370
290 IF D>F THEN 340
300 IF C>=1 THEN LOCATE 0,7,0:PRINT CHR$(48+C);:GOTO 320
310 LOCATE 0,7,0:PRINT B$;
320 GOSUB 1320:FOR GH=1 TO 500:NEXT GH:IF D<=F THEN 280
330 LOCATE 0,5,0:PRINT" -I Stay. ":GOSUB 80:IF Y=0 GOTO 200 ELSE GOTO 370
340 IF K=0 THEN GOTO 300
350 Z=10:LOCATE 0,7,0:PRINT"(X)":GOSUB 1320:IF D>7 THEN LOCATE 0,7,0:PRINT" -Total:";D;
360 LOCATE 0,5,0:PRINT"-I stay.        ";:FOR GH=1 TO 500:WE$=INKEY$:NEXT GH:LOCATE 0,5,0:PRINT"-Your turn.    ":CO=1:GOTO 210
370 LOCATE 0,2,0:PRINT"You are the dealer.":LOCATE 0,5,0:PRINT"-My cards:":K=1:Y=1
380 IF DI=1 THEN F=(3.5+2*RND(1))
390 IF DI=2 THEN F=(3+RND(1)+(A(0)+A(7)+A(8)+A(9))/(40*M/160))
400 IF DI=3 THEN F=(3+RND(1)+(A(0)+A(1)+A(2)+A(7)+A(8)+A(9))/(40*M/160)-(A(4)+A(5)+A(6))/(48*M/160))
410 IF F>7 THEN F=6
420 GOTO 280
1320 ON (A(Z) MOD 4)+1 GOTO 1330,1340,1350,1360
1330 CA$=CHR$(1)+CHR$(67):GOTO1370
1340 CA$=CHR$(1)+CHR$(68):GOTO1370
1350 CA$=CHR$(1)+CHR$(69):GOTO1370
1360 CA$=CHR$(1)+CHR$(70)
1370 W=W+2+(10 AND CA=0 AND CO=1): IF W>30 THEN W=0:H=2
1375 LOCATE W,9+H,0:PRINT CHR$(1)+CHR$(88);CHR$(1)+CHR$(87);CHR$(1)+CHR$(87);CHR$(1)+CHR$(87);CHR$(1)+CHR$(87);CHR$(1)+CHR$(87);CHR$(1)+CHR$(87);CHR$(1)+CHR$(87);CHR$(1)+CHR$(89);" ":IF CO=1 THEN CA=1
1380 FOR U=10+H TO 19+H:LOCATE W,U,0:PRINT CHR$(1)+CHR$(86):LOCATE W+8,U,0:PRINT CHR$(1)+CHR$(86):NEXT U
1390 LOCATEW,20+H,0:PRINT CHR$(1)+CHR$(90);CHR$(1)+CHR$(87);CHR$(1)+CHR$(87);CHR$(1)+CHR$(87);CHR$(1)+CHR$(87);CHR$(1)+CHR$(87);CHR$(1)+CHR$(87);CHR$(1)+CHR$(87);CHR$(1)+CHR$(91)
1400 FOR GH=10+H TO 19+H:LOCATE W+1,GH,0:PRINT"       ":NEXT GH
1410 IF Z<7 THEN LOCATE W+1,10+H,0:PRINT RIGHT$(STR$(Z+1),1):LOCATE W+1,11+H,0:PRINT CA$
1420 ON Z+1 GOSUB 1780,1790,1800,1810,1820,1830,1840,1850,1900,1940,2000
1430 RETURN
1780 LOCATE W+4,14+H,0:GOSUB 1970:RETURN
1790 LOCATE W+4,11+H,0:GOSUB 1970:LOCATE W+4,17+H,0:GOSUB 1970:RETURN
1800 LOCATE W+4,11+H,0:GOSUB 1970:LOCATE W+4,14+H,0:GOSUB 1970:LOCATE W+4,17+H,0:GOSUB 1970:RETURN
1810 LOCATE W+2,11+H,0:GOSUB 1970:LOCATE W+6,11+H,0:GOSUB 1970:LOCATE W+2,17+H,0:GOSUB 1970:LOCATE W+6,17+H,0:GOSUB 1970:RETURN
1820 LOCATE W+2,11+H,0:GOSUB 1970:LOCATE W+6,11+H,0:GOSUB 1970:LOCATE W+4,14+H,0:GOSUB 1970:LOCATE W+2,17+H,0:GOSUB 1970:LOCATE W+6,17+H,0:GOSUB 1970:RETURN
1830 LOCATE W+2,11+H,0:GOSUB 1970:LOCATE W+6,11+H,0:GOSUB 1970:LOCATE W+2,14+H,0:GOSUB 1970:LOCATE W+6,14+H,0:GOSUB 1970:LOCATE W+2,17+H,0:GOSUB 1970:LOCATE W+6,17+H,0:GOSUB 1970:RETURN
1840 LOCATE W+2,11+H,0:GOSUB 1970:LOCATE W+6,11+H,0:GOSUB 1970:LOCATE W+4,13+H,0:GOSUB 1970:LOCATE W+2,15+H,0:GOSUB 1970:LOCATE W+6,15+H,0:GOSUB 1970:LOCATE W+2,18+H,0:GOSUB 1970:LOCATE W+6,18+H,0:GOSUB 1970:RETURN
1850 LOCATE W+1,10+H,0
1860 PRINT "J"
1870 LOCATE W+1,11+H,0:PRINT CA$
1880 LOCATE W+3,12+H,0:PRINT CA$;CA$;CA$;CA$
1890 LOCATE W+6,13+H,0:GOSUB 1970:LOCATE W+6,14+H,0:GOSUB 1970:LOCATE W+3,15+H,0:GOSUB 1970:LOCATE W+6,15+H,0:GOSUB 1970:LOCATE W+3,16+H,0:GOSUB 1970:LOCATE W+6,16+H,0:GOSUB 1970:LOCATE W+4,17+H,0:PRINT CA$;CA$:RETURN
1900 LOCATE W+1,10+H,0:PRINT "Q"
1910 LOCATE W+1,11+H,0:GOSUB 1970
1920 LOCATE W+3,12+H,0:PRINT CA$;CA$;CA$
1930 FOR GH=13+H TO 16+H:LOCATE W+2,GH,0:GOSUB 1970:LOCATE W+6,GH,0:GOSUB 1970:NEXT GH:LOCATE W+4,15+H,0:GOSUB 1970:LOCATE W+3,17+H,0:PRINT CA$;CA$;CA$:LOCATE W+6,18+H,0:GOSUB 1970:RETURN
1940 LOCATE W+1,10+H,0:PRINT "K"
1950 LOCATE W+1,11+H,0:GOSUB 1970
1960 FOR GH=12+H TO 18+H:LOCATE W+2,GH,0:GOSUB 1970:NEXT GH
1965 LOCATE W+6,12+H,0:GOSUB 1970:LOCATE W+5,13+H,0:GOSUB 1970:LOCATE W+4,14+H,0:GOSUB 1970:LOCATE W+3,15+H,0:GOSUB 1970:LOCATE W+4,16+H,0:GOSUB 1970:LOCATE W+5,17+H,0:GOSUB 1970:LOCATE W+6,18+H,0:GOSUB 1970:RETURN
1970 PRINT CA$:RETURN
2000 LOCATE W+1,10+H,0:PRINT "X"
2010 FOR U=2 TO 7:LOCATE W+U,10+U+H,0:PRINT "X":NEXT U
2020 FOR U=7 TO 2 STEP -1:LOCATE W+U,19+H-U,0:PRINT "X":NEXT U:RETURN
3000 REM HELP
3010 PRINT "This card game uses packs of 40 cards.  Players play against the dealer. On each player's turn they may ask for extra   cards to improve their card total.";
3020 PRINT "The    final card remains face-down."
3030 PRINT "Card Values:Aces=1 point,1-7=Face value, Picture Cards: 1/2 point.    In this   video game  the three  picture cards are Jack, Queen and King (4 decks)."
3040 PRINT "If the cards exceed 7.5 points,they have gone bust, show  their cards  and loss the hand."
3050 PRINT "If  the  cards  are exactly 7.5 points, players show  their hand, their turn is finished and they will most likely win, unless the dealer has too 7.5."
3060 PRINT "If a dealer  goes  bust, then the other player that hasn't gone bust wins."
3070 PRINT "If a single player  scores  7.5 points, they  win and control the dealer on the next hand."
3080 PRINT "Press a key to continue..."
3100 OK$=INPUT$(1):CLS:RETURN
